//
//  Imandra Inc.
//  Copyright (c) 2024
//
//  Minimal FIX 4.4 model with receive logic using
//  a defined internal record to set "sent fields" 
//   
//  For further info see https://docs.imandra.ai
// 
// 

import FIX_4_4


// Import inbound NewOrderSingle message
message NewOrderSingle {
    req ClOrdID
    req Side
    req TransactTime
    req OrdType valid when it in [ OrdType.Limit, OrdType.Market ]
    req OrderQtyData.OrderQty
    opt Price

	validate { state.live_order == false } 
	
	validate { state.OrdStatus in [ OrdStatus.New ]}
}

// Import an outbound message ExecutionReport
outbound message ExecutionReport {
    req OrderID
    req ExecID
    req ExecType
    req OrdStatus
    req Side
    req LeavesQty
    req CumQty
    req AvgPx
    req OrderQtyData.OrderQty
}

// Define the interface state
internal state {

    // The following fields would be assigned to 
    assignable {
        OrdStatus   : OrdStatus = OrdStatus.New;
        Side        : Side = Side.Buy;
        OrderID     : string = "";
        LeavesQty   : Qty = 0.0;
        CumQty      : Qty = 0.0;
        AvgPx       : Price = 0.0;
        Price 		:? Price = None;
        OrdType		: OrdType = OrdType.Limit;
    }
    
    live_order : bool = false;
    
    // These are not required within the orders, etc...
    OrdQty : Qty = 0.0;
    filledQty : Qty = 0.0;
    current_time : UTCTimeOnly = UTCTimeOnly (8, 0, 0, None);
    
    // Define the opening and closing times
    opening_time : UTCTimeOnly = UTCTimeOnly (8, 0, 0, None);
    closing_time : UTCTimeOnly = UTCTimeOnly (16, 0, 0, None);

    //internal records
    OrderQtyData : OrderQtyData = {
        CashOrderQty = None;
        OrderPercent = None;
        RoundingDirection = None;
        RoundingModulus = None;
        OrderQty = Some 0.0;
    };
}


//  Define how the model should process a validated
//  incoming NewOrderSingle message
receive ( msg : NewOrderSingle ) {
	

    // Extract all relevant fields from within the message
    assignFrom (msg, state)
    
    state.live_order = true
    
    send ExecutionReport {
        state with 
            ExecID = "";
            ExecType = ExecType.New;
            // thus we still need to set this value to something in order to fulfil the requirement
            // that all the required fields are set
            OrderQtyData.OrderQty = state.CumQty;
    }
    
}
